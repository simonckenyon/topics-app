apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'kotlin-kapt'
apply plugin: "de.mannodermaus.android-junit5"

android {
    compileSdkVersion 28
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId 'ie.koala.topics'
        minSdkVersion 21
        targetSdkVersion 28

        resConfigs "en"

        def versionMajor = 1
        def versionMinor = 2
        def versionPatch = 0

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def versionPropsFile = file('version.properties')
        def code

        if (versionPropsFile.canRead()) {
            Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            code = versionProps['VERSION_CODE'].toInteger() + 1
            versionProps['VERSION_CODE'] = code.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new GradleException("Could not read version.properties!")
        }

        buildConfigField 'String', 'BUILD_TIME', "\"${buildTime()}\""
        buildConfigField 'String', 'GIT_HASH', "\"${gitSha()}\""

        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        versionCode code

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField 'String', "ApiKey", Topics_ApiKey
            resValue 'string', "api_key", Topics_ApiKey
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField 'String', "ApiKey", Topics_ApiKey
            resValue 'string', "api_key", Topics_ApiKey
        }

    }

    lintOptions {
        //checkReleaseBuilds false

        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }

    dataBinding {
        enabled true
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    buildToolsVersion = '28.0.3'
}

androidExtensions {
    experimental = true
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == "com.android.support") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "26.+"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'

    // Material Design
    implementation 'com.google.android.material:material:1.1.0-alpha01'

    // architecture components
    implementation 'androidx.paging:paging-runtime:2.1.0-beta01'
    implementation('android.arch.work:work-runtime:1.0.0-alpha11') {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0'

    // Room components
    implementation 'androidx.room:room-runtime:2.1.0-alpha02'
    kapt 'androidx.room:room-compiler:2.1.0-alpha02'

    // Navigation
    implementation "android.arch.navigation:navigation-fragment-ktx:1.0.0-alpha07"
    implementation "android.arch.navigation:navigation-ui-ktx:1.0.0-alpha07"

    // Preferences
    implementation 'androidx.preference:preference:1.1.0-alpha01'

    // Firebase
    implementation 'com.google.firebase:firebase-database:16.0.5'
    implementation 'com.google.firebase:firebase-core:16.0.4'
    implementation 'com.google.firebase:firebase-auth:16.0.5'
    implementation 'com.google.firebase:firebase-storage:16.0.5'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    implementation 'com.google.firebase:firebase-config:16.1.0'
    implementation 'com.google.firebase:firebase-iid:17.0.4'
    implementation 'com.firebaseui:firebase-ui-auth:4.2.1'
    implementation('com.firebaseui:firebase-ui:4.2.1') {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    // Anko
    implementation 'org.jetbrains.anko:anko:0.10.5'
    implementation 'org.jetbrains.anko:anko-commons:0.10.5'
    implementation 'org.jetbrains.anko:anko-design:0.10.5'

    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:retrofit-mock:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'

    // moshi
    implementation 'com.squareup.retrofit2:converter-moshi:2.4.0'
    implementation 'com.squareup.moshi:moshi:1.8.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.8.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.8.0'

    // glide
    implementation ('com.github.bumptech.glide:glide:4.8.0') {
        exclude group: 'com.android.support'
    }

    // Koin
    implementation 'org.koin:koin-android:1.0.1'

    // React (to be removed)
    implementation 'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:2.2.0'
    implementation 'com.androidhuman.rxfirebase2:firebase-database-kotlin:16.0.1.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'

    // misc
    implementation('info.bliki.wiki:bliki-core:3.1.0') {
        exclude group: 'com.github.tony19', module: 'logback-android'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    implementation 'net.opacapp:multiline-collapsingtoolbar:27.1.1'
    implementation files('libs/gravatar-android-1.0.jar')
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.hendraanggrian.pikasso:pikasso:0.1'
    implementation 'com.github.fondesa:kpermissions:1.0.0'
    implementation 'com.afollestad.material-dialogs:core:2.0.0-beta2'

    implementation(group: 'com.google.guava', name: 'guava', version: '26.0-android') {
        exclude group: 'com.google.guava' , module: 'failureaccess'
    }

    // logging
    api 'org.slf4j:slf4j-api:1.7.25'
    api 'org.slf4j:jul-to-slf4j:1.7.25'
    api 'com.github.tony19:logback-android:1.1.1-12'

    // testing
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    testImplementation 'org.mockito:mockito-core:2.23.0'

    // (Required) Writing and executing Unit Tests on the JUnit Platform
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

    // (Optional) If you need 'Parameterized Tests'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.1'

    // (Optional) If you also have JUnit 4-based tests
    testImplementation 'junit:junit:4.12'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.3.1'
}

static def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

static def buildTime() {
    return new Date().format("yyyy-MM-dd'T'HH:mm:ssZ", TimeZone.getTimeZone("UTC"))
}

apply plugin: 'com.google.gms.google-services'

